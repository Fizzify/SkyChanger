plugins {
    id 'net.minecrell.licenser' version '0.4.1'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
    id 'com.jfrog.bintray' version '1.8.3'
}

allprojects {
    description = 'Change the color of your personal sky.'
    group = 'com.dscalzi'

    ext {
        name = 'SkyChanger'
        author = 'Daniel Scalzi (TheKraken7)'
        url = 'https://github.com/dscalzi/skychanger'
    }
}

apply plugin: 'eclipse'

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'net.minecrell.licenser'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def major = '4'
    def minor = '0'
    def spongeRevision = '0'
    def bukkitRevision = '0'

    def rev

    if (project.name.endsWith('Sponge')) {
        rev = spongeRevision
    } else if (project.name.endsWith('Bukkit')) {
        rev = bukkitRevision
    } else {
        rev = spongeRevision.toInteger() > bukkitRevision.toInteger() ? spongeRevision : bukkitRevision
    }

    version = "${major}.${minor}.${rev}"
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    
    configurations {
        testImplementation.extendsFrom compileOnly
    }

    processResources {
        from "../LICENSE.txt"
    }

    license {
        sourceSets = [project.sourceSets.main]
        exclude '**/config.yml'
        header = rootProject.file('HEADER.txt')
        ext {
            name = 'Daniel D. Scalzi'
            year = '2017-2020'
            url = 'https://github.com/dscalzi/SkyChanger'
        }
    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jar {
        baseName = project.name

        manifest {
            attributes 'Implementation-Version': version, 'Specification-Version': version
        }
    }

    build {
        dependsOn clean
        dependsOn shadowJar

        jar.mustRunAfter clean
        shadowJar.mustRunAfter jar
    }

    bintrayUpload {
        dependsOn build
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ["BintrayRelease"]
        pkg {
            repo = 'maven'
            name = project.name
            licenses = ['MIT']
            vcsUrl = 'https://github.com/dscalzi/SkyChanger.git'
            publish = true
            version {
                name = project.version
                released = new Date()
                vcsTag = 'v' + project.version
            }
        }
    }

    publishing {
        publications {
            BintrayRelease(MavenPublication) {
                from components.java
                groupId group
                artifactId archivesBaseName
                version version
                artifact javadocJar
                artifact sourcesJar
            }
        }
    }
    
}