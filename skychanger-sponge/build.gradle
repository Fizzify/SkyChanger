import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.gradle.vanilla.repository.MinecraftPlatform
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'org.spongepowered.gradle.plugin' version '2.0.2'
    id 'org.spongepowered.gradle.vanilla' version '0.2'
    id 'java-library'
}

minecraft {
    version ('1.16.5')
    runs {
        platform(MinecraftPlatform.SERVER)
    }
}

sponge {
    apiVersion('8.0.0')
    license('MIT')
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version('1.0')
    }
    plugin('skychanger') {
        displayName(project.ext.name)
        version(project.version)
        entrypoint('com.dscalzi.skychanger.sponge.SkyChangerPlugin')
        description(project.description)
        links {
            homepage(project.ext.url)
            source(project.ext.url)
            issues("${project.ext.url}/issues")
        }
        contributor(project.ext.author) {
            description('Lead Developer')
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
    }
}

repositories {
    maven { url 'https://repo.codemc.org/repository/maven-public' }
}

dependencies {
    api project(':skychanger-core')
    implementation 'org.bstats:bstats-sponge:3.0.0'
}

shadowJar {
    relocate 'org.bstats', 'com.dscalzi.skychanger.sponge.lib.org.bstats'
    classifier 'dist'
}